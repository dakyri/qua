// Qua patch to play generatively to the EPS tabla instrument

qua Test1
{

	voice TablaRasa(
		short \range 0 127 vel=80,
		short \range -127 127 accent=25)
	{
		
		define Ta	note (36, 3, vel+accent)
		define ta	note (36, 3, vel)
		define keke	note (36, [1 1 1], [vel+accent vel vel])
		define ki	note (36, 1, vel)
		
		define Do	note (39, 3, vel+accent)
		define do	note (39, 3, vel)
		
		define Te	note (40, 3, vel+accent)
		define te	note (40, 3, vel)
		define tete	note (40, [1 1 1], [vel+accent vel vel])
		
		define da	note (42, 6, vel)
		define daa	note (42, 36, vel)
		define daaa	note (42, 48, vel)
		
		define dom	note (44, 6, vel)

		int		tempoto

		define \node \modal \toggle \reset bintal [
			findbeat
			if (roll(0.5)) [
				{daa Ta} te ta te
			] else [
				Ta te ta te
			]
			Ta te if (roll(0.5)) keke else ta te
			Te te te if (roll(0.5)) te else tete
			if (roll(0.5)) Ta else Te te te ta
			if (roll(0.5)) te else tete
		]
		
		define \node \modal \toggle \reset tintal [
			findbeat
			if (roll(0.5)) {daa Ta} else Ta ta te ta
			Ta te ta tete
			if (roll(0.5)) [
				Te te Ta ta
				Ta te tete te
			] else if (roll(0.5)) [
				Ta te keke
				Ta te keke
				Ta te keke
				Ta keke keke
				if (roll(0.5)) keke else te
				note(0, 6, 0)
				if (roll(0.5)) keke else te
				note(0, 6, 0)
				if (roll(0.5)) keke else te
				note(0, 6, 0)
				if (roll(0.5)) keke else te
				note(0, 6, 0)
				ta
			] else [
				Ta ta te ta
				{da Ta} keke {da keke} keke
			]
		]
		
		define \node \modal \toggle \reset noxtal [
			findbeat
			{daa Ta} te te ta
			Te keke ta te
			if (roll(0.5)) [
				{da Ta} te te te
			]
			if (roll(0.5)) [
				keke keke keke keke
			]
			if (roll(0.5)) [
				{daa Ta} te te keke te
				Ta te te keke Ta
				ta te
			]
			if (roll(0.5)) [
				{daa ta} ta te
				Ta ta te
				Ta keke te
				Ta keke ta
				{daa Ta} keke keke keke
			]
		]
		
		define \node \modal \toggle \reset frenzied [
			findbeat
			{da Ta} te te ta
			Te te ta te
			if (roll(0.5)) [
				{da ta} te keke te
			] else if (roll(0.5)) [
				ta te keke te
			]
		]

		define \oncer \toggle \reset \resetto 1 findbeat [
			(tick != 0) :: ki
		]


		define \node \toggle settempo(float \range 10 300 newTempo=80) {
			tempo = newTempo
		}

	//	settempo

		<- [
			bintal
			tintal
			noxtal
			frenzied
		]
	}

	channel channel1
	{
		input \midi in1 "In-A USB MidiSport 2x2":1
		output \midi out1 "Out-A USB MidiSport 2x2":1
	}

// main bit of sequencer
	
	region.start = 0:0.0
	region.duration = 32:0.0
	loop = false
	Tabla(channel1, 0, 32:0.0)
}
